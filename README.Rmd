---
output: github_document
bibliography: references.bib
---

```{r setup, include=FALSE}
knitr::opts_chunk$set(
  collapse = TRUE,
  comment = "#>",
  fig.path = "man/figures/README-",
  out.width = "100%"
)
# devtools::install_github("Hemken/Statamarkdown")
library(Statamarkdown)
```

# did2s

<!-- badges: start -->
<!-- badges: end -->

The goal of did2s is to estimate TWFE models without running into the problem caused by staggered treatment adoption. For details on the methodology, view this [vignette](http://kylebutts.com/did2s/articles/Two-Stage-Difference-in-Differences.html)

> [!WARNING]
>
> Due to the way Stata handles the `predict` command, users of this package should be careful when 
> they have always-treated units in their data or if all units end up treated before the end 
> of the panel. 
> If any value of `i.unit` or `i.year` are not estimated, then predict will estimate them as 0s
> which can predict very weird results. 

## Installation

You can install did2s from github with:

```stata
net install did2s, replace from("https://raw.githubusercontent.com/kylebutts/did2s_stata/main/ado/")
* ssc install did2s
```

## Two-stage Difference-in-differences [@Gardner_2021]

I have created an Stata package with the help of John Gardner to estimate the two-stage procedure. The command is `did2s` which estimates the two-stage did procedure. This function requires the following syntax

`did2s depvar [if] [in] [weight], first_stage(varlist) second_stage(varlist) treatment(varname) cluster(varname)`

- `first_stage`: formula for first stage, can include fixed effects and covariates, but do not include treatment variable(s)!
- `second_stage`: List of treatment variables. This could be, for example a 0/1 treatment dummy, a set of event-study leads/lags, or a continuous treatment variable
- `treatment`: This has to be the 0/1 treatment variable that marks when treatment turns on for a unit. If you suspect anticipation, see note above for accounting for this.
- `cluster`: Which variable to cluster on.


To view the documentation, type `help did2s` into the console.

## Example Usage


```{stata}
********************************************************************************
* Static
********************************************************************************

use data/df_het.dta
	
*-> two-stage manually (note standard errors are off)

* First-stage regression 
qui reg dep_var i.state i.year if treat == 0

* y_{it} - \hat{y}_{it}(\infty)
qui predict adj, residuals

* Second-stage regression
reg adj i.treat, cluster(state) nocons
```

```{stata, echo=-2}

use data/df_het.dta

*-> With did2s standard error correction	
did2s dep_var, first_stage(i.state i.year) second_stage(i.treat) treatment(treat) cluster(state)
```

You can also do event-study by changing the `second_stage`

```{stata, echo=c(-5, -6)}
********************************************************************************
* Dynamic
********************************************************************************

use data/df_het.dta

* can not have negatives in factor variable
qui gen rel_year_shift = rel_year + 20
qui replace rel_year_shift = 100 if rel_year_shift == .

did2s dep_var, first_stage(i.state i.year) second_stage(ib100.rel_year_shift) treatment(treat) cluster(state)
```


This method works with exogenous time-varying covariates as well!

```{stata}
********************************************************************************
* Castle Doctrine
********************************************************************************

use https://github.com/scunning1975/mixtape/raw/master/castle.dta, clear

* Define Covariates
global demo blackm_15_24 whitem_15_24 blackm_25_44 whitem_25_44

* No Covariates
did2s l_homicide [aweight=popwt], first_stage(i.sid i.year) second_stage(i.post) treatment(post) cluster(sid)

* Covariates
did2s l_homicide [aweight=popwt], first_stage(i.sid i.year $demo) second_stage(i.post) treatment(post) cluster(sid)
```


### Large Datasets or Many Fixed Effects

There are some situations where standard errors can not be calculate analytically in memory. The reason for this is that the analytic standard errors require the creation of the matrix containing all the fixed effects used in estimation. When there are a lot of observations and/or many fixed effects, this matrix can't be stored in memory.

In this case, it's possible to obtain standard errors via bootstrapping a custom program. Here is an example for the example data. You could spend time to make the command more programmable with args, but I find it easier to just write the estimation out.

```{stata}
use data/df_het.dta, clear

egen unique_id = group(state unit)

capture program drop did2s_est

program did2s_est, rclass
	version 13.0
	regress dep_var i.new_id i.year if treat == 0
	tempvar dep_var_resid
	predict `dep_var_resid', residuals
	regress `dep_var_resid' ib0.treat, nocons
end

xtset unique_id year
sort unique_id year
bootstrap, cluster(state) idcluster(new_id) group(unique_id) reps(100): did2s_est
```


## References

